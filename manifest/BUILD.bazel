load("@bazel_lib//lib:run_binary.bzl", "run_binary")

alias(
    name = "genmanifest",
    actual = "@com_avm99963_gomodules_webext//genmanifest",
)

# A template of the manifest adapted for the selected platform.
run_binary(
    name = "platform_manifest_template",
    srcs = ["manifest.template.gjson"],
    outs = ["manifest.template.json"],
    args = [
        "-template",
        "$(location manifest.template.gjson)",
        "-dest",
        "$(location manifest.template.json)",
    ] + select({
        "//:chromium": ["CHROMIUM_MV3"],
        "//:gecko": ["GECKO"],
    }),
    tool = "genmanifest",
)

sh_library(
    name = "manifest_utils",
    data = ["manifest_utils.sh"],
)

sh_binary(
    name = "stamp_manifest",
    srcs = ["stamp_manifest.sh"],
    deps = [
        ":manifest_utils",
        "//tools:release_utils",
        "@rules_shell//shell/runfiles",
    ],
)

run_binary(
    name = "manifest",
    srcs = [":platform_manifest_template"],
    outs = ["manifest.json"],
    args = select({
        "//:stable": ["stable"],
        "//:beta": ["beta"],
        "//:canary": ["canary"],
    }) + select({
        "//:chromium": ["chromium_mv3"],
        "//:gecko": ["gecko"],
    }) + [
        "$(location :platform_manifest_template)",
        "$(location manifest.json)",
    ],
    mnemonic = "StampManifest",
    progress_message = "Stamping manifest",
    stamp = -1,
    tool = ":stamp_manifest",
    visibility = ["//:__pkg__"],
)
