load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@rules_proto_grpc_doc//:defs.bzl", "doc_markdown_compile")
load("@rules_proto_grpc_js//:defs.bzl", "js_grpc_web_library")
load("//tools:common_js_project.bzl", "common_js_project")
load("//tools/preprocessor:bzl/preprocessor.bzl", "preprocessor")
load(":bzl/config.bzl", "enable_kill_switch_mechanism", "host")
load(":bzl/config_file.bzl", "config_file")

proto_library(
    name = "proto",
    srcs = [
        "api_proto/common.proto",
        "api_proto/kill_switch.proto",
        "api_proto/kill_switch_objects.proto",
    ],
    strip_import_prefix = "/src/killSwitch",
    deps = ["@protobuf//:timestamp_proto"],
)

js_grpc_web_library(
    name = "api_proto_generated",
    options = {
        "@rules_proto_grpc_js//:grpc_web_js_plugin": [
            "import_style=commonjs+dts",
            "mode=grpcwebtext",
        ],
        "@rules_proto_grpc_js//:proto_plugin": [
            "import_style=commonjs_strict",
        ],
    },
    output_mode = "NO_PREFIX",
    protos = [":proto"],
)

# We add @types/google-protobuf so that
# google-protobuf/google/protobuf/timestamp_pb is available to tsc when
# typechecking.
js_library(
    name = "api_proto",
    deps = [
        ":api_proto_generated",
        "//:node_modules/@types/google-protobuf",
    ],
)

config_file(
    name = "config",
    out = "config.js",
    host = ":host",
)

preprocessor(
    name = "index",
    src = "index.pre.js",
    out = "index.js",
    defined_dependencies = select({
        "//:gecko": ["GECKO"],
        "//conditions:default": [],
    }),
)

common_js_project(
    name = "killSwitch",
    srcs = glob(
        ["**/*.js"],
        exclude = [
            "**/*.test.js",
            "**/*.pre.js",
        ],
    ) + [
        ":config",
        ":index",
    ],
    visibility = [
        "//src/bg:__subpackages__",
        "//src/xhrInterceptor/killSwitchHandler:__subpackages__",
    ],
    deps = [
        ":api_proto",
        "//:node_modules/semver",
        "//src/common",
        "//src/common/options",
    ],
)

enable_kill_switch_mechanism(
    name = "enable_kill_switch_mechanism",
    build_setting_default = True,
)

config_setting(
    name = "should_enable_kill_switch_mechanism",
    flag_values = {
        "//src/killSwitch:enable_kill_switch_mechanism": "true",
    },
    visibility = [
        "//src/bg:__subpackages__",
        "//tools/preprocessor:__subpackages__",
    ],
)

host(
    name = "host",
    build_setting_default = "https://twpt-grpc-web.avm99963.com/",
)
